#
#= require jquery
#= require jquery_ujs
#= require leaflet
#= require leaflet.oms
#

class Map
  constructor: (id, options) ->
    @map = L.map(id).setView [51.505, -0.09], 13
    @map.addLayer L.tileLayer('http://{s}.tile.openstreetmap.org/{z}/{x}/{y}.png',
        attribution: 'Map data &copy; <a href="http://openstreetmap.org">OpenStreetMap</a> contributors, <a href="http://creativecommons.org/licenses/by-sa/2.0/">CC-BY-SA</a>',
        maxZoom: 18)
    @oms = new OverlappingMarkerSpiderfier @map,
      keepSpiderfied: true
      nearbyDistance: 20

    @markers = []
    @lightIcon = L.Icon.Default;
    @darkIcon = L.Icon.Default.extend({options: {iconUrl: '<%= image_path "marker-dark.png" %>'}});

    popup = new L.Popup offset: new L.Point(0.5, -24)
    @oms.addListener 'click', (marker) =>
      popup.setContent marker.desc
      popup.setLatLng marker.getLatLng()
      @map.openPopup popup

    @oms.addListener 'spiderfy', (markers) =>
      marker.setIcon new @lightIcon for marker in markers
      @map.closePopup()

    @oms.addListener 'unspiderfy', (markers) =>
      marker.setIcon new @darkIcon for marker in markers

    if options.markers?
      @addMarker m for m in options.markers

  addMarker: (options) =>
    marker = L.marker([options.lat, options.lng], { title: options.title, icon: new @darkIcon })
    marker.desc = "<a class=\"popup-link\" href=\"#{options.url}\">#{options.title}</a><br />"

    @map.addLayer marker
    @oms.addMarker marker
    @markers.push marker

  adjustToMarkers: ->
    bounds = new L.LatLngBounds(marker.getLatLng() for marker in @markers)

    @map.fitBounds bounds
    if @map.getZoom() > 12
      @map.setZoom 12

$ ->
  $(".map").each ->
    map = new Map $(@).data("map"), markers: $(@).data("markers")
    map.adjustToMarkers()
